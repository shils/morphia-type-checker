apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

group               = 'me.shils'
version             = '0.1.0'
targetCompatibility = 1.6

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.4.4'
  compile 'org.mongodb.morphia:morphia:1.0.1'
  compile 'org.ow2.asm:asm-all:5.0.4'
  testCompile 'junit:junit:4.11'
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

def pomConfig = {
  scm {
    url 'https://github.com/shils/morphia-type-checker'
    connection 'scm:git:https://github.com/shils/morphia-type-checker.git'
    developerConnection 'scm:git:git@github.com:shils/morphia-type-checker.git'
  }
  licenses {
    license {
      name 'The Apache Software License, Version 2.0'
      url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      distribution 'repo'
    }
  }
  developers {
    developer {
      id 'S'
      name 'Shil Sinha'
      email 'shil.sinha@gmail.com'
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourcesJar {
        classifier 'sources'
      }

      artifact javadocJar {
        classifier 'javadoc'
      }

      pom.withXml {
        def root = asNode()
        root.appendNode('name', 'Morphia Type Checker')
        root.appendNode('description', 'A set of Groovy type checking extensions for Morphia.')
        root.appendNode('url', 'https://github.com/shils/morphia-type-checker')
        root.appendNode('inceptionYear', '2015')
        root.children().last() + pomConfig
      }
    }
  }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

uploadArchives.dependsOn test
check.dependsOn jacocoTestReport

