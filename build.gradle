plugins {
  id "com.jfrog.bintray" version "1.3.1"
}
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

group               = 'me.shils'
version             = '0.1.0'
targetCompatibility = 1.7

repositories {
  mavenCentral()
  jcenter()
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.4.6'
  compile 'org.mongodb.morphia:morphia:1.0.1'
  compile 'com.intellij:annotations:12.0'
  compile 'org.ow2.asm:asm-all:5.0.4'
  testCompile 'junit:junit:4.11'
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

if( !hasProperty( 'bintrayUser' ) ) {
  ext.bintrayUser = ''
}

if( !hasProperty( 'bintrayKey' ) ) {
  ext.bintrayKey = ''
}

def pomConfig = {
  scm {
    url 'https://github.com/shils/morphia-type-checker'
    connection 'scm:git:https://github.com/shils/morphia-type-checker.git'
    developerConnection 'scm:git:git@github.com:shils/morphia-type-checker.git'
  }
  licenses {
    license {
      name 'The Apache Software License, Version 2.0'
      url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      distribution 'repo'
    }
  }
  developers {
    developer {
      id 'S'
      name 'Shil Sinha'
      email 'shil.sinha@gmail.com'
    }
  }
}

bintray {
  user = bintrayUser
  key = bintrayKey
  publications = ['mavenJava']
  pkg {
    repo = 'maven'
    name = 'morphia-type-checker'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/shils/morphia-type-checker.git'
    version {
      name = '0.1.0'
      desc = 'Morphia Type Checker 0.1.0'
      released = new Date()
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourcesJar {
        classifier 'sources'
      }

      artifact javadocJar {
        classifier 'javadoc'
      }

      pom.withXml {
        def root = asNode()
        root.appendNode('name', 'Morphia Type Checker')
        root.appendNode('description', 'Compile time Morphia validation using Groovy type checking extensions.')
        root.appendNode('url', 'https://github.com/shils/morphia-type-checker')
        root.appendNode('inceptionYear', '2015')
        root.children().last() + pomConfig
      }
    }
  }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

uploadArchives.dependsOn test
check.dependsOn jacocoTestReport

